module probs 
import immutable/list

extern pure def pow(x: Double, y: Double): Double = "Math.pow(x, y)"

effect Sample(dist: Distribution): Double
effect Observe(value: Double, dist: Distribution): Unit
effect Random(): Double
effect Weight(prob: Probability): Unit
interface Emit[A] {
  def emit(element: A): Unit
}

type Distribution{
  Gaussian(mean: Double, variance: Double)
  Uniform(lower: Double, upper: Double)
  Beta(alpha: Double, beta: Double)
}
type Probability = Double
type Trace = List[Double]

// draws a Sample from a Distribution
def draw(dist: Distribution): Double / Random = {
  dist match {
    case Gaussian(mean, variance) => 
      val u1 = do Random();
      val u2 = do Random();

      val sigma = variance

      //box-muller-Methode
      val mag = sigma * sqrt(-2.0 * log(u1))
      val z0 = mag * cos(2.0 * PI * u2) 
      //val z1 = mag * sin(2.0 * PI * u2)

      return z0 + mean
    
    case Uniform(lower, upper) => 
      val x = do Random()
      return (lower + x * upper)
    
    case Beta(alpha, beta) => 
      val mean = 0.5
      val variance = 1.0 /(4.0 * ((2.0 * alpha) + 1.0))
      return draw(Gaussian(mean, variance))
  } 
}

def gamma(z0: Double): Double = {
  var z = z0
  //g is a small integer and p ist liste von coeficients dependent on g
  val g = 7.0 
  val p = [0.99999999999980993,
    676.5203681218851,
    -1259.1392167224028,
    771.32342877765313,
    -176.61502916214059,
    12.507343278686905,
    -0.13857109526572012,
    9.9843695780195716 * pow(10.0, 0.0-6.0),
    1.5056327351493116 * pow(10.0, 0.0-7.0)] 

  // takes the i-th element of a list
  def takeith(i: Double, p0: List[Double]): Double = {
    p0 match {
      case Nil() => panic("empty list")
      case Cons(p, ps) =>
        if(i == 1.0) {p}
        else {takeith(i - 1.0, ps)}
    }
  }

  //lanczos approximation
  if (z < 0.5) {
    val y = PI / (sin(PI * z) * gamma(1.0 - z))
    return y
  } else {
    z = z - 1.0
    var x = takeith(1.0, p)
    var i = 2.0
    while(i <= 9.0){
      x = x + (takeith(i, p) / (z + i - 1.0))
      i = i + 1.0
    }
    val t = z + g + 0.5
    val y = sqrt(2.0 * PI) * pow(t, (z + 0.5)) * exp(0.0 - t) * x
    return y
  }
}

//probability density function for Gaussian distribution
def density(value: Double, dist: Distribution): Double = {
  dist match {
    case Gaussian(mean, variance) =>
      val density = 1.0/(variance * (sqrt(2.0 * PI))) * exp(0.0 - (square(value - mean)/square(variance))/ 2.0)
      return density
    
    case Uniform(lower, upper) => 
      if(lower < value && value < upper){ 1.0 / (upper - lower)}
      else {0.0}
    
    case Beta(alpha, beta) =>
      val density = (gamma(alpha + beta)/(gamma(alpha) * gamma(beta))) * pow(value, (alpha - 1.0)) * pow((value - 1.0), (beta - 1.0))
      return density
  }
}

// default Sample und Observe handler
def handleSample[R]{program: () => R / Sample} = {
  try {program()}
  with Sample {(dist) => resume(draw(dist))}
}

def handleObserve[R]{program: () => R / Observe} = {
  try {program()}
  with Observe {(value, dist) => do Weight(density(value, dist)); resume(())}
}

def handleRandom[R]{program: () => R / Random}: R = {
  try {program()}
  with Random {() => resume(random())}
}

//rejection sampling
def rejectionSampling[R]{program: () => R / Weight}: R / Random = {
  try {program()}
  with Weight {(prob) => 
    if(do Random() < prob) {resume(())}
    else {rejectionSampling{program}}
  }
}

def handleWeight[R]{program: () => R / Weight}: (R, Double)  = {
  var current = 1.0
  try {(program(), current)}
  with Weight {(prob) =>
    current = current * prob;
    resume(())}
}

//Metropolis Hastings auxiliary definitions and inference Operations
// wenn sample dann nächstes element sonst panic("message")
def handleReusingTrace[R](trace0: Trace){program: () => R / Sample} = {
  var trace = trace0
  try {program()}
  with Sample {(dist) =>
    trace match {
      case Nil() => panic("empty trace")
      case Cons(t, ts) => trace = ts; resume(t) 
    }
  }
}

// wenn sample dann sample + in trace speichern
//resune nach cons -> falsche reihenfolge
//def handleTracing[R]{program: () => R / Sample}: (R, Trace) / Sample = {
//  var trace = []
//
//  try {(program(), trace)}
//  with Sample {(dist) => 
//    val sample = do Sample(dist);
//    trace = Cons(sample, trace)
//    resume(sample);
//  }
//}

def handleTracing[R]{program: () => R / Sample}: (R, Trace) / Sample = {
  try {(program(), [])}
  with Sample {(dist) => 
    val d = do Sample(dist);
    val (r, trace) = resume(d);
    (r, Cons(d, trace))
  }
}

def handleEmit[A,R]{program: () => R / Emit[A]} = {
  try {program()}
  with Emit[A] {
    def emit(element) = {println(element); resume(())}}
}

def handleLimitEmit[A,R](n: Int){program: () => R / Emit[A]} = {
  var steps = n
  try {program(); ()}
  with Emit[A] {
    def emit(element) = {
      println(element)
      steps = steps - 1
      if (0 < steps) {resume(())}
    }
  }
}

// generatin proposal by adding gausain noise(normal noise)
// adding noise to all rekursiv -> independent MH
def propose(trace: Trace): Trace / Sample = {
  trace match {
    case Nil() => [] 
    case Cons(t, ts) => 
      val noise = do Sample(Gaussian(0.0, 1.0))
      val proposal = t + noise
      Cons(proposal, propose(ts)) 
  }
}

//generation proposal for single-site MH
// adding noise to only one sample per iteration otherwise reuse trace
def proposeSingleSite(trace: Trace): Trace / Sample = {
  val tsize = toDouble(size(trace))
  val rand = do Sample(Uniform(1.0, tsize))
  var result = []

  def putAti(i: Double, p0: List[Double]): List[Double] = {
    p0 match {
      case Nil() => result
      case Cons(p, ps) =>
        if(i == 1.0) {
          val noise = do Sample(Gaussian(0.0, 1.0))
          result = append(result, [p + noise])
          putAti(i - 1.0, ps)
        }
        else {
          result = append(result, [p])
          putAti(i - 1.0, ps)
        }
    }
  }

  putAti(toDouble(floor(rand)), trace)
}

// 1 iteration/step of Metropolis-Hastings algorithm
def metropolisStep[A](prob0: Probability, trace0: Trace){program: Trace => (A, Probability)} = {
  val trace1 = propose(trace0)
  val (result1, prob1) = program(trace1)

  if(prob1 < prob0){
    do Weight(prob1 / prob0)
  }
  ((result1, trace1), prob1)
}

def metropolisStepSingleSite[A](prob0: Probability, trace0: Trace){program: Trace => (A, Probability)} = {
  val trace1 = proposeSingleSite(trace0)
  val (result1, prob1) = program(trace1)

  if(prob1 < prob0){
    do Weight(prob1 / prob0)
  }
  ((result1, trace1), prob1)
}

def metropolisHastings[A]{program: () => A / {Sample, Weight}} = {
  val ((result0, trace0), prob0) = handleWeight{
    handleRandom{
      handleSample{
        handleTracing{
          program()
        }
      }
    }
  }

  //var nextprob = prob0
  //var nexttrace = trace0
  //var nextresult = result0

  //n mal metropolis step um nächste trace & prob zu bekommen
  //handleWeight bei jedem aufruf von metropolisStep
  //handleRandom handleSample auch bei jedem aufruf 
  //while (true) {
  //  //mStep
  //  val ((nextresult, nexttrace), nextprob) =
  //  handleRandom{
  //    handleSample{
  //      rejectionSampling{
  //        metropolisStep(nextprob, nexttrace){ trace => handleWeight { handleReusingTrace(trace) { program() } } }
  //      }
  //    }
  //  }
  //  do emit(nextresult)
  //}

  def loop(result: A, trace: Trace, prob: Probability): Unit / Emit[A] = {
    do emit(result)
    val ((result1, trace1), prob1) =
    handleRandom{
      handleSample{
        rejectionSampling{
          metropolisStep(prob, trace){ trace => handleWeight { handleReusingTrace(trace) { program() } } }
        }
      }
    }
    loop(result1, trace1, prob1)
  }

  loop(result0, trace0, prob0)
}

def metropolisHastingsSingleSite[A]{program: () => A / {Sample, Weight}} = {
  val ((result0, trace0), prob0) = handleWeight{
    handleRandom{
      handleSample{
        handleTracing{
          program()
        }
      }
    }
  }

  def loop(result: A, trace: Trace, prob: Probability): Unit / Emit[A] = {
    do emit(result)
    val ((result1, trace1), prob1) =
    handleRandom{
      handleSample{
        rejectionSampling{
          metropolisStepSingleSite(prob, trace){ trace => handleWeight { handleReusingTrace(trace) { program() } } }
        }
      }
    }
    loop(result1, trace1, prob1)
  }  

  loop(result0, trace0, prob0)
}

//MCMC algorithm for drawing 'random' numbers from distribution
def sliceSampling[R](){program: () => R / Weight} = {
  val (result, prob) = handleWeight{program()}
  
  def step(result0: R, prob0: Probability) = rejectionSampling{
    val (result1, prob1) = handleWeight{program()}
    if (prob1 < prob0) {do Weight(prob1 / prob0)}
    (result1, prob1)
  }

  def loop(result: R, prob: Probability): Unit / Emit[R] = {
    do emit(result)
    val (result1, prob1) = step(result, prob)
    loop(result1, prob1)
  }

  loop(result, prob)
}

def main() = {
  // testing the draw function
  //repeat(50){
  //  val dr = handleRandom{
  //    draw(Gaussian(0.0, 1.0))
  //  }
  //  println(dr)
  //}

  // testing handleReusingTrace
  //val res = handleReusingTrace([0.7, 2.1]){ () => 
  //  val a = do Sample(Gaussian(0.0, 1.0))  
  //  return a
  //}
  //println(res)
  //println("\n")

  // testing Metropolis-Hastings
  println("testing MH on Gaussian distribution with mean=0, variance=1")
  handleLimitEmit[Double,Unit](10){metropolisHastings{() => 
      do Sample (Gaussian(0.0, 1.0))
    }
  }

  println("\n testing SingleSite MH on Gaussian distribution with mean=0, variance=1")
  handleLimitEmit[Double,Unit](10){metropolisHastingsSingleSite{() => 
      do Sample (Gaussian(0.0, 1.0))
    }
  }

  // testing sliceSampling
  println("\n testing Silce sampling on Gaussian distribution with mean=0, variance=1")
  handleLimitEmit[Double, Unit](10){handleRandom{handleSample{sliceSampling{
    do Sample(Gaussian(0.0, 1.0))
  }}}}
}